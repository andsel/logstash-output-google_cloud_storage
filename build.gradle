import java.nio.file.Files
import java.nio.file.Paths

import static java.nio.file.StandardCopyOption.REPLACE_EXISTING

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven-publish'

group "org.logstash.inputs"
version Files.readAllLines(Paths.get("version")).first()

repositories {
    mavenCentral()
}

dependencies {
    // Core dependency for Logstash Output Google Cloud Storage
    implementation "com.google.cloud:google-cloud-storage:2.12.0"

    implementation "com.fasterxml.jackson.core:jackson-core:2.13.4"
    implementation "com.google.api-client:google-api-client:2.0.0"
    implementation "com.google.api.grpc:proto-google-common-protos:2.9.2"
    implementation "com.google.api.grpc:proto-google-iam-v1:1.5.2"
    implementation "com.google.api:api-common:2.2.1"
    implementation "com.google.api:gax-httpjson:0.104.1"
    implementation "com.google.api:gax:2.19.1"
    implementation "com.google.apis:google-api-services-storage:v1-rev20220705-2.0.0"
    implementation "com.google.auth:google-auth-library-credentials:1.11.0"
    implementation "com.google.auth:google-auth-library-oauth2-http:1.11.0"
    implementation "com.google.auto.value:auto-value-annotations:1.9"
    implementation "com.google.cloud:google-cloud-core-http:2.8.12"
    implementation "com.google.cloud:google-cloud-core:2.8.12"
    implementation "com.google.code.findbugs:jsr305:3.0.2"
    implementation "com.google.code.gson:gson:2.9.1"
    implementation "com.google.guava:failureaccess:1.0.1"
    implementation "com.google.guava:guava:31.1-jre"
    implementation "com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava"
    implementation "com.google.http-client:google-http-client-apache-v2:1.42.2"
    implementation "com.google.http-client:google-http-client-appengine:1.42.2"
    implementation "com.google.http-client:google-http-client-gson:1.42.2"
    implementation "com.google.http-client:google-http-client-jackson2:1.42.2"
    implementation "com.google.http-client:google-http-client:1.42.2"
    implementation "com.google.j2objc:j2objc-annotations:1.3"
    implementation "com.google.oauth-client:google-oauth-client:1.34.1"
    implementation "com.google.protobuf:protobuf-java-util:3.21.6"
    implementation "com.google.protobuf:protobuf-java:3.21.6"
    implementation "io.grpc:grpc-context:1.49.0"
    implementation "io.opencensus:opencensus-api:0.31.1"
    implementation "io.opencensus:opencensus-contrib-http-util:0.31.1"
    implementation "javax.annotation:javax.annotation-api:1.3.2"
    implementation "org.checkerframework:checker-qual:3.25.0"
    implementation "org.threeten:threetenbp:1.6.1"
}

task generateGemJarRequiresFile {
    doLast {
        File jars_file = file('lib/logstash-output-google_cloud_storage_jars.rb')
        jars_file.newWriter().withWriter { w ->
            w << "# AUTOGENERATED BY THE GRADLE SCRIPT. DO NOT EDIT.\n\n"
            w << "require \'jar_dependencies\'\n"
            configurations.runtimeClasspath.allDependencies.each {
                w << "require_jar(\'${it.group}\', \'${it.name}\', \'${it.version}\')\n"
            }
            w << "require_jar(\'${project.group}\', \'${project.name}\', \'${project.version}\')\n"
        }
    }
}

task vendor {
    doLast {
        String vendorPathPrefix = "vendor/jar-dependencies"
        configurations.runtimeClasspath.allDependencies.each { dep ->
            File f = configurations.runtimeClasspath.filter { it.absolutePath.contains("${dep.group}/${dep.name}/${dep.version}") }.singleFile
            String groupPath = dep.group.replaceAll('\\.', '/')
            File newJarFile = file("${vendorPathPrefix}/${groupPath}/${dep.name}/${dep.version}/${dep.name}-${dep.version}.jar")
            newJarFile.mkdirs()
            Files.copy(f.toPath(), newJarFile.toPath(), REPLACE_EXISTING)
        }
        String projectGroupPath = project.group.replaceAll('\\.', '/')
        File projectJarFile = file("${vendorPathPrefix}/${projectGroupPath}/${project.name}/${project.version}/${project.name}-${project.version}.jar")
        projectJarFile.mkdirs()
        Files.copy(file("$buildDir/libs/${project.name}-${project.version}.jar").toPath(), projectJarFile.toPath(), REPLACE_EXISTING)
    }
}

vendor.dependsOn(jar, generateGemJarRequiresFile)